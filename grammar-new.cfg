S -> CODE
CODE -> VDECL CODE | FDECL CODE | CDECL CODE | ϵ
VDECL -> vtype id semi | vtype ASSIGN semi
ASSIGN -> id assign RHS
RHS -> EXPR | literal | character | boolstr
EXPR -> EXPR addsub TERM | TERM
TERM -> TERM multdiv FACTOR | FACTOR
FACTOR -> lparen EXPR rparen | id | num
FDECL -> vtype id lparen ARG rparen lbrace BLOCK RETURN rbrace
ARG -> vtype id MOREARGS | ϵ
MOREARGS -> comma vtype id MOREARGS | ϵ
BLOCK -> STMT BLOCK | ϵ
STMT -> VDECL | ASSIGN semi
STMT -> if lparen COND rparen lbrace BLOCK rbrace ELSE
STMT -> while lparen COND rparen lbrace BLOCK rbrace
COND -> COND comp boolstr | boolstr
ELSE -> else lbrace BLOCK rbrace | ϵ
RETURN -> return RHS semi
CDECL -> class id lbrace ODECL rbrace
ODECL -> VDECL ODECL | FDECL ODECL | ϵ
